10 PRINT TAB(33);"WORDLE"
15 PRINT TAB(29);"KATIE ANDERSON"
16 PRINT TAB(21);"BASED ON WORDLE BY JOSH WARDLE"
20 PRINT:PRINT:PRINT
25 PRINT "IN THIS GAME, YOU GET SIX TRIES TO GUESS THE 5 LETTER WORD THE I"
30 PRINT "AM THINKING OF. AFTER EACH GUESS, LETTERS THAT ARE IN THE CORRECT"
35 PRINT "LOCATION ARE MARKED WITH A ! AND LETTERS THAT ARE CORRECT BUT IN"
40 PRINT "THE WRONG LOCATION ARE MARKED WITH A ?. LETTERS THAT ARE NOT IN"
45 PRINT "THE WORD ARE MARKED WITH AN X."
50 PRINT "TYPE K TO SEE A VIRTUAL KEYBOARD MARKED WITH !, ? AND X."
55 PRINT
60 DIM K$(26),R$(6)
70 READ C
80 FOR I=1 TO RND(C)
90 READ S$
100 NEXT I
110 FOR I=1 TO 26:K$(I)=" ":NEXT
130 C=1
140 IF C>6 GOTO 600
150 INPUT G$
160 IF G$="K" GOTO 500
170 IF LEN(G$)<>5 PRINT "YOUR GUESS MUST BE 5 LETTERS LONG.":GOTO 150
180 IF G$=S$ GOTO 620
185 R$(C)=""
190 FOR I=1 TO 5
195 L$="X"
200 IF MID$(G$,I,1)=MID$(S$,I,1) THEN L$="!":GOTO 290
210 N=0:M=0:X=0
220 FOR J=1 TO I
230 IF MID$(G$,I,1)=MID$(G$,J,1) THEN N=N+1
240 NEXT J
250 FOR J=1 TO 5
260 IF MID$(G$,I,1)=MID$(S$,J,1) AND MID$(G$,J,1)<>MID$(S$,J,1) THEN M=M+1
265 IF M>=N THEN X=1
270 NEXT J
280 IF X=1 THEN L$="?":GOTO 290
290 PRINT L$;
295 R$(C)=R$(C)+L$
300 Q=ASC(MID$(G$,I,1))-64
310 IF L$="!" THEN K$(Q)="!"
320 IF L$="?" AND K$(Q)<>"!" THEN K$(Q)="?"
330 IF L$="X" AND K$(Q)=" " THEN K$(Q)="X"
340 NEXT I
350 PRINT:PRINT G$
360 C=C+1
370 GOTO 140
500 PRINT
510 FOR I=1 TO 26
520 PRINT K$(I);
530 NEXT I
540 PRINT:PRINT "ABCDEFGHIJKLMNOPQRSTUVWXYZ":PRINT
550 GOTO 150
600 PRINT "OUT OF TRIES."
605 C=6
610 GOTO 630
620 IF C=1 THEN PRINT "GENIUS!"
621 IF C=2 THEN PRINT "MAGNIFICENT!"
622 IF C=3 THEN PRINT "IMPRESSIVE!"
623 IF C=4 THEN PRINT "SPLENDID!"
624 IF C=5 THEN PRINT "GREAT!"
625 IF C=6 THEN PRINT "PHEW!"
626 R$(C)="!!!!!"
630 PRINT "THE SECRET WORD WAS ";S$;"!":PRINT
640 FOR I=1 TO C
650 PRINT R$(I)
660 NEXT I
670 PRINT
680 PRINT "WOULD YOU LIKE TO PLAY AGAIN? Y/N"
690 INPUT R$
700 IF R$="Y" THEN RESTORE:GOTO 70
1000 DATA 5,"ABACK","ABASE","ABATE","ABBEY","ABBOT"
